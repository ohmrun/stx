package stx.g.denote;

//using stx.g.Lang;
import eu.ohmrun.pml.PExpr in TPExpr;

class PExpr<T>{
  public final inner : T -> GExpr;
  public function new(inner){
    this.inner = inner;
  }
  public function apply(self:TPExpr<T>){
    final e = __.g().expr();
    return switch(self){
      case PLabel(name)      : 
        e.Call(
          e.Path('eu.ohmrun.pml.PExpr.PExprSum.PLabel'),
          [e.Const(c -> c.String(name))]
        );
      case PGroup(list)      : 
        final xs = list.toIter().foldr(
          (n,m) -> e.Call(
            e.Path('stx.ds.LinkedList.LinkedListSum.Cons'),
            [
              apply(n),m
            ]
          ),
          e.Path('stx.ds.LinkedList.LinkedListSum.Nil')
        );
        e.Call(
          e.Path('eu.ohmrun.pml.PExpr.PExprSum.PGroup'),
          [xs]
        );
      case PValue(value)     : 
        e.Call(
          e.Path('eu.ohmrun.pml.PExpr.PExprSum.PValue'),
          [inner(value)]
        );
      case PEmpty:
        e.Path('eu.ohmrun.pml.PExpr.PExprSum.PEmpty');
    }
  }
}